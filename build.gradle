plugins {
    id "architectury-plugin" version "3.4-SNAPSHOT"
    id "dev.architectury.loom" version "1.0-SNAPSHOT" apply false
    id "io.github.pacifistmc.forgix" version "1.2.2"
}
apply plugin: "maven-publish"

repositories {
    gradlePluginPortal()
}

architectury {
    minecraft = rootProject.minecraft_version
}

version = "${mod_version}"

println "Mod version: $version"

@SuppressWarnings('unused')
Object getModDescription() {
    return "${mod_description}"
}

forgix {
    group = "com.ultreon.mods"
    mergedJarName = "ultreon-lib-${version}.jar"
    outputDir = "build/libs/merged"

    //noinspection GroovyAssignabilityCheck
    forge {
        projectName = "merge-forge"
        jarLocation = "build/libs/ultreon-lib-${version}.jar"
    }

    //noinspection GroovyAssignabilityCheck
    fabric {
        projectName = "merge-fabric"
        jarLocation = "build/libs/ultreon-lib-${version}.jar"
    }
}

tasks.register('collectJars') {
    dependsOn "mergeJars"
    dependsOn ":forge:build"
    dependsOn ":fabric:build"
    dependsOn ":common:build"

    doFirst {
        delete fileTree("$buildDir/libs/collected")
        mkdir "$buildDir/libs/collected"
        copy {
            from "$projectDir/common/build/libs/$archivesBaseName-${version}.jar"
            into "$buildDir/libs/collected"
            rename ".*", "$archivesBaseName-common-${version}.jar"
        }
        copy {
            from "$projectDir/forge/build/libs/$archivesBaseName-${version}.jar"
            into "$buildDir/libs/collected"
            rename ".*", "$archivesBaseName-forge-${version}.jar"
        }
        copy {
            from "$projectDir/fabric/build/libs/$archivesBaseName-${version}.jar"
            into "$buildDir/libs/collected"
            rename ".*", "$archivesBaseName-fabric-${version}.jar"
        }
        copy {
            from "$buildDir/libs/merged/$archivesBaseName-${version}.jar"
            into "$buildDir/libs/collected"
        }
    }
}

mergeJars {
    finalizedBy "collectJars"
    dependsOn ":merge-forge:build"
    dependsOn ":merge-fabric:build"
}

println "forgix.outputDir = $forgix.outputDir"

subprojects {
    apply plugin: "dev.architectury.loom"
    apply plugin: "maven-publish"

    loom {
        silentMojangMappingsLicense()
    }

    repositories {
        mavenCentral()
        google()

        mavenLocal()

        maven {
            name 'Sponge / Mixin'
            url 'https://repo.spongepowered.org/repository/maven-public/'
        }

        maven {
            name 'BlameJared Maven (CrT / Bookshelf)'
            url 'https://maven.blamejared.com'
        }
        maven {
            name 'ParchmentMC'
            url 'https://maven.parchmentmc.org'
        }

        maven {
            name 'Quilt (Release)'
            url 'https://maven.quiltmc.org/repository/release/'
        }

        maven {
            name 'Jitpack'
            url 'https://jitpack.io/'
        }

        maven {
            name "Cursemaven"
            url "https://cursemaven.com"
            content {
                includeGroup "curse.maven"
            }
        }
        maven {
            // location of the maven that hosts JEI files
            name "Progwml6 maven"
            url "https://dvs1.progwml6.com/files/maven/"
        }
        maven {
            name "Fuzs Mod Resources"
            url "https://raw.githubusercontent.com/Fuzss/modresources/main/maven/"
        }
        maven { url "https://maven.architectury.dev/" }
        maven { url 'https://dl.cloudsmith.io/public/geckolib3/geckolib/maven/' }
    }

    dependencies {
        minecraft "com.mojang:minecraft:${rootProject.minecraft_version}"
        // The following line declares the mojmap mappings, you may use other mappings as well
        mappings loom.layered() {
            officialMojangMappings()
            parchment("org.parchmentmc.data:parchment-1.19.2:2022.08.10@zip")
        }

        compileOnly 'com.google.errorprone:error_prone_annotations:2.16'
    }

    processResources {
        exclude "*.pdn", "*.xcf", "*.bbmodel", "*.bin", "*.rom"
    }
}

allprojects {
    apply plugin: "java"
    apply plugin: "architectury-plugin"
    apply plugin: "maven-publish"

    archivesBaseName = rootProject.archives_base_name
    version = rootProject.version
    group = rootProject.maven_group

    repositories {
        // Add repositories to retrieve artifacts from in here.
        // You should only use this when depending on other mods because
        // Loom adds the essential maven repositories to download Minecraft and libraries from automatically.
        // See https://docs.gradle.org/current/userguide/declaring_repositories.html
        // for more information about repositories.
        maven { url = "https://maven.parchmentmc.org" }

        flatDir {
            dir "${rootProject.projectDir}/libs"
        }
    }

    tasks.withType(JavaCompile).tap {
        configureEach {
            options.encoding = "UTF-8"
            options.release.set(17 as Integer)
        }
    }

    java {
        withSourcesJar()
    }
}

mergeJars.mustRunAfter build
//build.dependsOn mergeJars
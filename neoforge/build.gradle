plugins {
    id "com.github.johnrengelman.shadow" version "7.1.2"
}

version rootProject.version

architectury {
    platformSetupLoomIde()
    neoForge()
}

loom {
    neoForge {

    }
}

configurations {
    common
    shadowCommon // Don't use shadow from the shadow plugin because we don't want IDEA to index this.
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentNeoForge.extendsFrom common
}

repositories {
    maven { url "https://maven.neoforged.net/releases" }
}

dependencies {
    neoForge "net.neoforged:neoforge:$neoforge_version"
    // Remove the next line if you don't want to depend on the API
    modApi "dev.architectury:architectury-neoforge:${architectury_version}"

    common(project(path: ":common", configuration: "namedElements")) { transitive false }
    shadowCommon(project(path: ":common", configuration: "transformProductionNeoForge")) { transitive = false }

    include api("io.github.ultreon:ubo:$ultreon_data_version")

    include api("io.github.ultreon.corelibs:collections-v0:$corelibs_version")
    include api("io.github.ultreon.corelibs:commons-v0:$corelibs_version")
    include api("io.github.ultreon.corelibs:crash-v0:$corelibs_version")
    include api("io.github.ultreon.corelibs:datetime-v0:$corelibs_version")
    include api("io.github.ultreon.corelibs:events-v1:$corelibs_version")
    include api("io.github.ultreon.corelibs:functions-v0:$corelibs_version")
    include api("io.github.ultreon.corelibs:registries-v0:$corelibs_version")
    include api("io.github.ultreon.corelibs:resources-v0:$corelibs_version")
    include api("io.github.ultreon.corelibs:text-v0:$corelibs_version")
    include api("io.github.ultreon.corelibs:translations-v0:$corelibs_version")

}

processResources {
    inputs.property "version", rootProject.version
    inputs.property "description", rootProject.getModDescription()
    inputs.property "loaderVersion", rootProject.neoforge_version.toString().split("\\.")[0]
    inputs.property "neoForgeVersion", rootProject.neoforge_version
    inputs.property "minecraftVersion", rootProject.architectury.minecraft

    filesMatching("META-INF/mods.toml") {
        HashMap<String, ?> p = new HashMap<>()
        p.put("version", rootProject.version)
        p.put("description", rootProject.getModDescription())
        p.put("loaderVersion", rootProject.neoforge_version.toString().split("\\.")[0])
        p.put("neoForgeVersion", rootProject.neoforge_version)
        p.put("minecraftVersion", rootProject.architectury.minecraft)
        expand(p)
    }
}

shadowJar {
    exclude "fabric.mod.json"
    exclude "architectury.common.json"

    configurations = [project.configurations.shadowCommon]
    archiveClassifier = "dev-shadow"
}

remapJar {
    //noinspection GrDeprecatedAPIUsage
    input.set shadowJar.archiveFile
    dependsOn shadowJar
    archiveClassifier = null
}

jar {
    archiveClassifier = "dev"
    manifest {
        attributes([
                "Specification-Title"     : "Ultreon Modding Lib",
                "Specification-Vendor"    : "Ultreon Team",
                "Specification-Version"   : "1", // We are version 1 of ourselves
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : project.jar.archiveVersion,
                "Implementation-Vendor"   : "Ultreon Team",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                "MixinConfigs"           : "ultreonlib-common.mixins.json"
        ])
    }
}

sourcesJar {
    def commonSources = project(":common").sourcesJar
    dependsOn commonSources
    from commonSources.archiveFile.map { zipTree(it) }
}

components.java {
    withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
        skip()
    }
}

publishing {
    publications {
        //noinspection GroovyAssignabilityCheck
        mavenNeoForge(MavenPublication) {
            artifactId = rootProject.archives_base_name + "-" + project.name
            //noinspection GroovyAssignabilityCheck
            from components.java
        }
    }

    // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
    repositories {
        // Add repositories to publish to here.
    }
}
